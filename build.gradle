apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'signing'

group = 'kz.greetgo'

sourceCompatibility = 1.8
version = '0.1.0'

def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")

jar {
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': version
    attributes 'Implementation-Vendor': 'greetgo!'
    attributes 'Created-By': System.getProperty("user.name")
    attributes 'Created-At': nowFormattedDate
  }
}
eclipse.project.natures 'org.springsource.ide.eclipse.gradle.core.nature'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["src", "src_resources"]
sourceSets.test.java.srcDirs = ["test_src"]
sourceSets.test.resources.srcDirs = ["test_src", "test_resources"]

eclipse {
  jdt {
    //
    // if you want to alter the java versions (by default they are configured with
    // gradle java plugin settings):
    //
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
}

project.eclipse.classpath.file.whenMerged { cp ->
  if (project.hasProperty("sourceCompatibility")) {
    cp.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {

      it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/" +
        "JavaSE-$project.sourceCompatibility"
    }
  }
}

def ggRepo = System.getenv('GG_REPO')

repositories {
  mavenLocal()
  if (ggRepo?.trim()) maven { url ggRepo.trim() }
  mavenCentral()
}

dependencies {
  ext.springVersion = "3.0.7.RELEASE"

  compile 'com.google.code.findbugs:annotations:2.0.1'

  compile 'javax.mail:mail:1.4.7'
  compile 'org.easytesting:fest-assert-core:2.0M10'

  testCompile 'org.testng:testng:6.5.1'

}

test { useTestNG() }

task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.archives
    repository(
      url: System.getenv('GG_REPO')
    )
    pom.project {
      name project.name
      packaging 'jar'
    }
  }
}

artifacts {
  archives javadocJar, sourcesJar
}

if (System.getenv('LIB_SIGN_GPG_KEY_ID') != null && System.getenv('LIB_SIGN_GPG_KEY_ID').length() > 0) {

  gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
      allprojects { ext."signing.keyId" = System.getenv('LIB_SIGN_GPG_KEY_ID') }
      allprojects { ext."signing.secretKeyRingFile" = System.getenv('LIB_SIGN_GPG_KEY_LOCATION') }
      allprojects { ext."signing.password" = System.getenv('LIB_SIGN_GPG_KEY_PASSWORD') }
    }
  }

}

signing {
  sign configurations.archives
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('encoding', 'UTF-8')
  options.addStringOption('charSet', 'UTF-8')
}

project.tasks.create('uploadToMavenCentral', Upload.class) {
  configuration = configurations.archives
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(
          userName: System.getenv('LIB_SONATYPE_ACCOUNT_HASH_ID'),
          password: System.getenv('LIB_SONATYPE_ACCOUNT_HASH_PASSWORD')
        )
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(
          userName: System.getenv('LIB_SONATYPE_ACCOUNT_HASH_ID'),
          password: System.getenv('LIB_SONATYPE_ACCOUNT_HASH_PASSWORD')
        )
      }

      pom.project {
        name project.name
        packaging 'jar'

        description 'Fast sending email util'
        url 'http://tech.greetgo.kz/libs/greetgo/email.php'

        scm {
          connection 'scm:git:https://github.com/greetgo/email'
          developerConnection 'scm:git:https://github.com/greetgo/email'
          url 'https://github.com/greetgo/email'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'greetgo'
            name 'Evgeniy Kolpakov'
            email 'tech@greet-go.com'
          }
          developer {
            id 'yakzhanov'
            name 'Yerlan Akzhanov'
            email 'yakzhanov@greet-go.com'
          }
          developer {
            id 'mkasyanov'
            name 'Maxim Kasyanov'
            email 'mkasyanov@greet-go.com'
          }

        }
      }
    }
  }
}


