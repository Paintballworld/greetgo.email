apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'kz.greetgo'

sourceCompatibility = 1.7
version = '1.0.2'

def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': version, 'Implementation-Vendor': 'greetgo!',
      'Built-By': System.getProperty("user.name"), 'Built-At': nowFormattedDate
  }
}

eclipse {
  jdt {
    //
    // if you want to alter the java versions (by default they are configured with
    // gradle java plugin settings):
    //
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
  }
  
  pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
}

project.eclipse.classpath.file.whenMerged { cp ->
  if (project.hasProperty("sourceCompatibility")) {
    cp.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {
      
      it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/" +
          "JavaSE-$project.sourceCompatibility"
    }
  }
}

repositories {
  maven {
    name 'Our repository'
    url 'http://192.168.0.1:8080/repository/internal/'
  }
  mavenCentral()
}

dependencies {
  ext.springVersion = "3.0.7.RELEASE"
  
  compile "org.springframework:spring-jdbc:${springVersion}"
  
  //compile 'org.slf4j:log4j-over-slf4j:1.7.5'
  compile 'com.metapossum:metapossum-scanner:1.0.1'
  compile 'kz.greetgo:greetgo.sqlmanager:1.0.2'
  
  testCompile 'org.testng:testng:6.1.1'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  
  testCompile 'kz.greetgo:greetgo.conf:0.0.12'
  
  testCompile 'postgresql:postgresql:9.1-901.jdbc4'
  testCompile 'c3p0:c3p0:0.9.1.2'
  
  testCompile "greetgo.3rd-party:oracle-ojdbc6_g:11.2.0.3"
  testCompile "greetgo.3rd-party:oracle-ucp:11.2.0.3"
}

test { useTestNG() }

task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.archives
    repository(
      url: 'http://192.168.0.1:8080/repository/internal/'
    )
    
    pom.project {
      name project.name
      packaging 'jar'
    }
  }
}
